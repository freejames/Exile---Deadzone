/**
 * ExileServer_system_trading_network_purchaseItemRequest
 *
 * Exile Mod
 * www.exilemod.com
 * Â© 2015 Exile Mod Team
 *
 * This work is licensed under the Creative Commons Attribution-NonCommercial-NoDerivatives 4.0 International License. 
 * To view a copy of this license, visit http://creativecommons.org/licenses/by-nc-nd/4.0/.
 Modified by [TAG] dekela for XP Tax
 */
 
private["_qualityModifier","_playerUID","_respectModifier","_newplayerRespect","_sessionID", "_parameters", "_itemClassName", "_quantity", "_containerType", "_containerNetID", "_playerObject", "_vehicleObject", "_salesPrice", "_playerMoney", "_playerRespect", "_quality", "_requiredRespect", "_logging", "_traderLog", "_responseCode"];
_sessionID = _this select 0;
_parameters = _this select 1;
_itemClassName = _parameters select 0;
_quantity = _parameters select 1;
_containerType = _parameters select 2;
_containerNetID = _parameters select 3;
try 
{
    _playerObject = _sessionID call ExileServer_system_session_getPlayerObject;
    if (_playerObject getVariable ["ExileMutex",false]) then
    {
        throw 12;
    };
    _playerObject setVariable ["ExileMutex",true];
    _vehicleObject = objNull;
    if (isNull _playerObject) then
    {
        throw 1;
    };
    if !(alive _playerObject) then
    {
        throw 2;
    };
    if !(isClass (missionConfigFile >> "CfgExileArsenal" >> _itemClassName) ) then
    {
        throw 3;
    };
    _salesPrice = getNumber (missionConfigFile >> "CfgExileArsenal" >> _itemClassName >> "price");
    if (_salesPrice <= 0) then
    {
        throw 4;
    };
    _playerMoney = _playerObject getVariable ["ExileMoney", 0];
    if (_playerMoney < _salesPrice) then
    {
        throw 5;
    };
    _playerRespect = _playerObject getVariable ["ExileScore", 0];
    _quality = getNumber(missionConfigFile >> "CfgExileArsenal" >> _itemClassName >> "quality");
    _requiredRespect = getNumber(missionConfigFile >> "CfgTrading" >> "requiredRespect" >> format["Level%1",_quality]);
    if (_playerRespect < _requiredRespect) then
    {
        throw 14;
    };
    
    ///-XP Tax-///
    _qualityModifier = (_quality/100);
    _respectModifier = _salesPrice*_qualityModifier;
    missionNamespace setVariable ["respectModifier",_respectModifier];
    publicVariable "respectModifier";
    _newplayerRespect = round (_playerRespect - _respectModifier);
    if (_newplayerRespect < 0) then 
    {
    _newplayerRespect = 0;
    };
    _playerObject setVariable ["ExileScore", _newplayerRespect];
    _playerUID = getPlayerUID _playerObject;
    format["setAccountScore:%1:%2", _newplayerRespect, getPlayerUID _playerObject] call ExileServer_system_database_query_fireAndForget;
    [_playerUID, "purchaseVehicleSkinResponse", [0, str _newplayerRespect]] call ExileServer_system_network_send_to;
    ExileClientPlayerScore = _newplayerRespect;
    (owner _playerObject) publicVariableClient "ExileClientPlayerScore";
    ExileClientPlayerScore = nil;
    ///-XP Tax-///    
    
    _playerMoney = _playerMoney - _salesPrice;
    _playerObject setVariable ["ExileMoney", _playerMoney, true];
    format["setPlayerMoney:%1:%2", _playerMoney, _playerObject getVariable ["ExileDatabaseID", 0]] call ExileServer_system_database_query_fireAndForget;
    [_sessionID, "purchaseItemResponse", [0, _salesPrice, _itemClassName, 1, _containerType, _containerNetID]] call ExileServer_system_network_send_to;
    _logging = getNumber(configFile >> "CfgSettings" >> "Logging" >> "traderLogging");
    if (_logging isEqualTo 1) then
    {
        _traderLog = format ["PLAYER: ( %1 ) %2 PURCHASED ITEM %3 FOR %4 POPTABS | PLAYER TOTAL MONEY: %5",getPlayerUID _playerObject,_playerObject,_itemClassName,_salesPrice,_playerMoney];
        "extDB2" callExtension format["1:TRADING:%1",_traderLog];
    };
    if !(_vehicleObject isEqualTo objNull) then
    {
        _vehicleObject call ExileServer_object_vehicle_database_update;
    }
    else 
    {
        _playerObject call ExileServer_object_player_database_update;
    };
}
catch 
{
    _responseCode = _exception;
    [_sessionID, "purchaseItemResponse", [_responseCode, 0, "", 0, 0, ""]] call ExileServer_system_network_send_to;
};    
_playerObject setVariable ["ExileMutex",false];
true